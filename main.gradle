allprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: 'org.sonarqube'
    apply plugin: "org.owasp.dependencycheck"

    sourceCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
    }

    group 'com.github.bancolombia'

    sonarqube {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.projectKey", "bancolombia_bin-stash"
            property "sonar.organization", "grupo-bancolombia"
            property "sonar.host.url", "https://sonarcloud.io/"
            property "sonar.sources", "src/main"
            property "sonar.test", "src/test"
            property "sonar.java.binaries", "build/classes"
            property "sonar.junit.reportPaths", "build/test-results/test"
            property "sonar.java-coveragePlugin", "jacoco"
            property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/generateMergedReport/generateMergedReport.xml"
            property "sonar.exclusions", ".github/**"
            property 'sonar.coverage.exclusions', 'examples/**/*'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {

    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.owasp.dependencycheck"

    ext {
        groupId = 'com.github.bancolombia'
    }

    group groupId

    repositories {
        mavenCentral()
    }

    dependencies {

        testImplementation(platform('org.junit:junit-bom:5.7.0'))

        implementation 'io.projectreactor:reactor-core'
        compileOnly 'org.projectlombok:lombok:1.18.28'
        annotationProcessor 'org.projectlombok:lombok:1.18.28'
        testCompileOnly 'org.projectlombok:lombok:1.18.28'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
        testImplementation 'io.projectreactor:reactor-test'

        testImplementation('org.junit.jupiter:junit-jupiter')
        testImplementation 'org.mockito:mockito-inline:3.7.7'
        testImplementation 'org.mockito:mockito-junit-jupiter:3.7.7'
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.apache.logging.log4j') {
                details.useVersion '2.20.0'
            }
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    test.finalizedBy(project.tasks.jacocoTestReport)

    jacocoTestReport {
        dependsOn test
        reports {
            xml.setRequired true
            xml.setOutputLocation file("${buildDir}/reports/jacoco.xml")
            csv.setRequired false
            html.setOutputLocation file("${buildDir}/reports/jacocoHtml")
        }
    }
    compileJava {
        options.compilerArgs = [
                '-Amapstruct.suppressGeneratorTimestamp=true',
                '-Amapstruct.suppressGeneratorVersionInfoComment=true'
        ]
    }

    check.dependsOn dependencyCheckAggregate

    dependencyCheck {
        format = 'JSON'
        formats = ['JSON']
        scanProjects = [':bin-stash-centralized',':bin-stash-hybrid', ':bin-stash-local']
    }

    if (toPublish.split(',').contains(it.name)) {
        apply plugin: "maven-publish"
        apply plugin: "signing"

        tasks.register('sourcesJar', Jar) {
            dependsOn classes
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        javadoc.failOnError = false
        tasks.register('javadocJar', Jar) {
            dependsOn javadoc
            archiveClassifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }

        tasks.withType(GenerateModuleMetadata).configureEach {
            enabled = false
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {

                    pom {
                        name = 'Bin-Stash'
                        description = 'Caching data in memory and/or a centralized cache to build a two tier stage cache.'
                        url = 'https://github.com/bancolombia/bin-stash'
                        licenses {
                            license {
                                name = "MIT License"
                                url = "https://opensource.org/licenses/mit-license.php"
                                distribution = "repo"
                            }
                        }
                        developers {
                            developer {
                                id = "gabheadz"
                                name = "Gabriel Martinez"
                                email = "gabrieljm@gmail.com"
                            }
                        }
                        scm {
                            url ="git@github.com:bancolombia/bin-stash.git"
                        }
                    }

                    from components.java

                    artifact sourcesJar
                    artifact javadocJar

                    groupId = groupId
                    artifactId = artifactId
                    version = project.property('version')
                }
            }
        }

        if (project.hasProperty('signing.keyId')) {
            signing {
                sign publishing.publications.mavenJava
            }
        }
    }

}

tasks.register('generateMergedReport', JacocoReport) {
    dependsOn test
    dependsOn subprojects.test
    dependsOn subprojects.javadoc
    dependsOn subprojects.jacocoTestReport
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.setRequired true
        csv.setRequired false
        html.setRequired true
    }
}

tasks.named('wrapper') {
    gradleVersion = '8.1.1'
}
